// Copyright (c) 2014 Leonardo Laguna Ruiz
// License: MIT

// This file contains implementations of a few trigonometric functions
// that are part of the Vult runtime
// Most of this functions were obtained with Mathematica. e.g.
// HornerForm[Evaluate[Normal[Series[Tanh[x], {x, 0, 11}]]]]

fun sin(x0){
   val x1    = x0 % (2.0*3.14159265359);
   val sign  = x1 > 3.14159265359;
   val x2    = x1 % 3.14159265359;
   val x3    = if x2 > 3.14159265359/2.0 then 3.14159265359 - x2 else x2;
   val xp2   = x3 * x3;
   val acc   = x3 * (1.0 + (-1.0/6.0 + 1.0/120.0 * xp2) * xp2);
   return if sign then -acc else acc;
}

fun cos(x0){
   return sin(x0 + 3.14159265359/2.0);
}


fun tanh(x){
   val xp2 = x * x;
   return x*(1.0+xp2 *((-1.0/3.0)+xp2*(2.0/15.0+xp2*((-17.0/315.0)+xp2*(62.0/2835.0-1382.0/155925.0*xp2)))));
}

fun tan(x){
   val xp2 = x * x;
   return x*(1.0+xp2 *((1.0/3.0)+xp2*(2.0/15.0+xp2*((17.0/315.0)+xp2*(62.0/2835.0+1382.0/155925.0*xp2)))));
}

// Alternative implementation. Uses a fixed polynomial size
// Compared to the version in vultin.c this one avoids
// using division. The polynomial is expanded at 3.5 which is
// better for pitch calculations.
// Mathematica: HornerForm[Evaluate[Normal[Series[Exp[x],{x,3.5,8}]]]]
fun exp(x) {
   val acc = -0.01642631545570055 + 0.0008213157727850276 * x;
   acc = 0.16672710187536058 + x * acc;
   acc = -0.9716165592046876 + x * acc;
   acc = 3.769326074725287  + x * acc;
   acc = -9.079440539195287 + x * acc;
   acc = 15.035981849082908 + x * acc;
   acc = -12.20233976850298 + x * acc;
   acc = 6.292667998164617  + x * acc;
   return acc;
}

