

fun adsr(gate:bool, a,d,s,r) : real {
   mem state;
   mem out;
   mem rate;
   mem target;
   // Magic numbers:
   // 0.001 : scales the effect of input parameters
   // 1024.0 : improves numeric precision in fixed-point

   out = out + (target - out) * rate * 0.001;

   // idle // release
   if(state == 0) {
      if(gate) state = 1; // goto attack
      rate = r;
      target = 0.0;
   }
   // attack
   if(state == 1) {
      if(not(gate)) state = 0; // goto release
      if(out > 1024.0) state = 2; // goto decay
      rate = a;
      target = 1.2 * 1024.0;
   }
   // decay // sustain
   if (state == 2) {
      if(not(gate)) state = 0; // goto release
      rate = d;
      target = s * 1024.0;
   }

   return out/1024.0;
}
