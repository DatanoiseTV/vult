
fun min(a,b) return if a<b then a else b;
fun max(a,b) return if a>b then a else b;

fun clip(value,low,high){
    return min(max(low,value),high);
}

fun abs(x) return if x<0 then -x else x;

fun near_zero(x) : bool return abs(x)<1e-12;

fun samplerate() return 44100;
fun PI() return 3.141592653589793;

fun dcblock(x0){
  mem x1,y1;
  val y0 = x0-x1+y1*0.995;
  x1,y1 = x0,y0;
  return y0;
}

fun pitchToRate(d) return 8.1758*exp(0.0577623*d)/samplerate();

fun pulse_train(m,phase){
  val pi_phase = phase * PI();
  val denominator1 = sin(pi_phase);
  val tmp1 = 0;
  if(near_zero(denominator1)) {
    tmp1 = 1;
  }
  else {
    tmp1 = sin(m * pi_phase);
    tmp1 =  tmp1 / (m * denominator1);
  }
  return tmp1;
}

// Main process function
fun osc(pitch,pw,wave:int){
  mem m:int;
  mem rate;
  mem phase;
  mem p;
  mem pre_pitch;
  mem state_triang;
  mem state_pulse;
  mem state_saw;
  mem triang;
  mem pre_wave:int;
  mem output;

  val fixed_pitch = 0;
  if(wave != pre_wave && abs(output) < 0.05) {
    pre_wave = wave;
  }
  if(pre_wave == 0 || pre_wave == 1) {
    fixed_pitch = pitch;
  }
  else {
    fixed_pitch = pitch + 12;
  }
  // Updates the parameters if the pre_pitch changed
  if(pre_pitch != fixed_pitch) {
    pre_pitch = fixed_pitch;
    rate = pitchToRate(pitch);
    p = 1/rate;
    val maxHarmonics = floor(p/4);
    m = 2 * maxHarmonics + 1;
  }
  // Generates a shifted version of the phase
  val shift05 = 0.5 + pw * 0.49;
  val shift = phase + shift05;
  if(shift > 1) {
    shift = shift - 1;
  }
  // Generates the first pulse train
  val tmp1 = pulse_train(m,phase);
  // Generates the second pulse train
  val tmp2 = pulse_train(m,shift);
  // Updates the phase
  phase = phase + rate;
  if(phase > 1) {
    phase = phase - 1;
  }

  // Calculates the waveforms based on the pulse trains
  state_pulse = clip(state_pulse * 0.9995 + tmp1 - tmp2, -1, 1);
  state_saw = clip(state_saw * 0.9995 + (tmp1 + tmp2  - 2 / p)/shift05/2,-1,1);
  state_triang = clip(state_triang * 0.9995 + 2 * state_pulse/p,-1,1);

  // Select the wave to output
  if(pre_wave == 0) {
    output = state_pulse;
  }
  else if(pre_wave == 1) {
    output = state_saw;
  }
  else if(pre_wave == 2) {
    output = 2*state_triang;
  }
  output = dcblock(output);
  return output;
}

